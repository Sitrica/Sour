plugins {
	id 'eclipse'
	id 'java-library'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
}

project.version = "1.1.2-e"

repositories {
	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

}

dependencies {
	implementation project(':common')

	// Bungeecord
	compileOnly (group: 'net.md-5', name: 'bungeecord-api', version: '1.17-R0.1-SNAPSHOT')

	// Adventure + Serializers (Provided by platform specific version)
	compileOnly (group: 'net.kyori', name: 'adventure-api', version: '4.9.3')

	// Configurate (Provided by loader specific version)
	compileOnly (group: 'org.spongepowered', name: 'configurate-core', version: '4.1.2')
}

shadowJar {
	archivesBaseName = "SourCore-Bungee"
	archiveClassifier.set("")

	minimize {
		exclude(project(':common'))
	}

	relocate 'com.sitrica.core.common', 'com.sitrica.core.bungee'
}

publishing {
	repositories {
		maven {
			name = "SourCore"
			url = uri("https://maven.pkg.github.com/Sitrica/Sour")
			credentials {
				username = 'Sitrica'
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
			}
		}
	}
	publications {
		shadow(MavenPublication) {publication ->
			project.shadow.component(publication)
			groupId = 'com.sitrica.core'
			artifactId = 'bungee'
		}
	}
}
