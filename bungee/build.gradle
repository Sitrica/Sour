plugins {
	id 'eclipse'
	id 'java-library'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

jar.archiveName = "SourCore-Bungee.jar"
project.version = "1.1.2"

repositories {
	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

}

dependencies {
	compile project(':common')

	// Bungeecord
	compileOnly (group: 'net.md-5', name: 'bungeecord-protocol', version: '1.16-R0.4-SNAPSHOT')
	compileOnly (group: 'net.md-5', name: 'bungeecord-api', version: '1.17-R0.1-SNAPSHOT')
}

shadowJar {
	classifier = ''
	archiveFileName.set("SourCore-Bungee.jar")

	minimize {
		exclude(project(':common'))
	}

	relocate 'com.sitrica.core.common', 'com.sitrica.core.bungee'
}

publishing {
	repositories {
		maven {
			name = "SourCore"
			url = uri("https://maven.pkg.github.com/Sitrica/Sour")
			credentials {
				username = 'Sitrica'
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
			}
		}
	}
	publications {
		shadow(MavenPublication) {publication ->
			project.shadow.component(publication)
			groupId = 'com.sitrica.core'
			artifactId = 'bungee'
		}
	}
}