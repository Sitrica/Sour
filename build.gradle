import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '5.2.0'
	id 'eclipse'
	id 'java-library'
	id 'maven-publish'
}

jar.archiveName = project.name + '.jar'

repositories {
	mavenCentral()

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

}

dependencies {

	// Bungeecord
	implementation (group: 'net.md-5', name: 'bungeecord-protocol', version: '1.15-SNAPSHOT')
	implementation (group: 'net.md-5', name: 'bungeecord-api', version: '1.15-SNAPSHOT')

	shadow (group: 'com.google.code.gson', name: 'gson', version: '2.8.6')

//	shadow fileTree(dir: 'libraries', include: '*.jar')
}

publishing {
	repositories {
		maven {
			name = project.name
			url = uri("https://maven.pkg.github.com/Sitrica/" + project.name)
			credentials {
				username = 'Sitrica'
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			version = project.property("version")
			groupId = 'com.sitrica'
			artifactId = project.name
			from(components.java)
		}
	}
}

processResources {
	from (sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: ["version": project.property("version")]
		include '**/*.yml'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = ''
	baseName = project.name
	classifier = ''
	minimize()
}
